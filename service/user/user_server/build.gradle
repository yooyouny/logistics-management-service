plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.google.cloud.tools.jib' version '3.3.2'
}

group = 'com.sparta'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2023.0.3")
}

dependencies {
    implementation project(':common:domain')
    implementation project(':service:user:user_dto')
    implementation project(':service:gateway:gateway_domain')

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:5.0.0:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

jib {
    from {
        image = "amazoncorretto:17"
        platforms {
            platform {
                architecture = 'arm64'
                os = 'linux'
            }
        }
    }
    to {
        image = "${project.name}".toLowerCase() + ":${project.version}"
    }
    container {
        ports = ['19092']
        jvmFlags = [
                "-server",
                "-Xmx512M",
                "-Dspring.profiles.active=local",
                "-XX:+UseContainerSupport",
                "-XX:InitialRAMPercentage=60.0",
                "-XX:MaxRAMPercentage=60.0",
                "-XX:+UseZGC",
                "-XX:+DisableExplicitGC",
                "-XX:MetaspaceSize=256M",
                "-XX:MaxMetaspaceSize=256M",
                "-Duser.timezone=UTC",
                "-Djava.net.preferIPv4Stack=true",
                "-Djava.net.preferIPv6Addresses=false",
                "-Dnetworkaddress.cache.ttl=0",
                "-Dnetworkaddress.cache.negative.ttl=0",
        ]
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

///// Querydsl 빌드 옵션 (옵셔널)
def generated = 'src/main/generated'

///// querydsl QClass 파일 생성 위치를 지정
tasks.withType(JavaCompile) {
    options.getGeneratedSourceOutputDirectory().set(file(generated))
}

///// java source set 에 querydsl QClass 위치 추가
sourceSets {
    main.java.srcDirs += [generated]
}

///// gradle clean 시에 QClass 디렉토리 삭제
clean {
    delete file(generated)
}